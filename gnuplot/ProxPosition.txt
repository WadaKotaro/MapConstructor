OutputTerm = 3
Env = "nc2021"
Tag = "SPROX"
BaseName = (Env."_").Tag
set encoding utf8
angle = 120*pi/180
X(x,y) = (x*cos(angle)-y*sin(angle))
Y(x,y) = (x*sin(angle)+y*cos(angle))

if (OutputTerm  == 1){
	set term pngcairo color size 2000,2000 enhanced font ",24"
	set output ("fig/".BaseName)."_pp.png"
}
if (OutputTerm  == 2){
	set term postscript eps color size 4.5in,3in enhanced font ",18"
	set output ("fig/".BaseName)."_pp.eps"
}
if (OutputTerm  == 3){
	set term svg size 1000,1000 font ",36"
	set output ("fig/".BaseName)."_pp.svg"
}

set xlabel 'x [m]' offset 0,1.1
set ylabel 'y [m]' offset 2,0

set tmargin 0
set bmargin 0
set lmargin 0
set rmargin 0
set size 0.88,0.88
set origin 0.10,0.10
set xtics offset 0,0.4
set ytics offset 0.8,0

#set nokey
set xrange [70:110]
set xtics 10
set yrange [-40:0]
set ytics 10
#set key left top
#set key right top
#set key left bottom
#set key right bottom
set key at 92,-32
set key invert
set size ratio -1

array proxClr[3] = ["#0000ff", "#800080", "#ff0000"]
array Title[3] = ["Time series data A", "Time series data B", "Time series data C"]

plot for [i = 3:1:-1] sprintf("..\/data\/%s_%d_%s_absprox.csv", Env, i, Tag) using (X($3,$4)):(Y($3,$4)) with point ps 0.3 pt 7 lc rgb proxClr[i] title Title[i]